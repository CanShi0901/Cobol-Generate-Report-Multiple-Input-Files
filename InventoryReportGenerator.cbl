       IDENTIFICATION DIVISION.
       PROGRAM-ID. GENERATE-INVENTORY-REPORT.
       AUTHOR. CAN SHI.
       
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT INVENTORY-FILE-IN ASSIGN TO 'INVENT.TXT'
           ORGANIZATION IS LINE SEQUENTIAL.
           SELECT OPTIONAL INVENTORY-REPORT-OUT ASSIGN TO 'INVREPRT.TXT'
           ORGANIZATION IS LINE SEQUENTIAL.
           SELECT SUPPLIER-FILE-IN ASSIGN TO 'SUPPLIERS.TXT'
           ORGANIZATION IS LINE SEQUENTIAL.
           SELECT OPTIONAL REORDER-REPORT-OUT ASSIGN TO 'REOREPRT.TXT'
           ORGANIZATION IS LINE SEQUENTIAL.
           
       DATA DIVISION.
       FILE SECTION.
       FD INVENTORY-FILE-IN.
       01 RECORD-IN.
           05 PART-NUMBER PIC 9(5).
           05 PART-NAME PIC X(20).
           05 QUANTITY-ON-HAND PIC 9(3).
           05 UNIT-PRICE PIC 99V99.
           05 INV-SUPPLIER-CODE PIC X(5).
           05 REORDER-POINT PIC 9(3).
           
       FD INVENTORY-REPORT-OUT.
       01 REPORT-CONTENT PIC X(50).
       
       FD SUPPLIER-FILE-IN.
       01 SUPPLIER-RECORD.
           05 SUPPLIER-CODE PIC X(5).
           05 SUPPLIER-NAME PIC X(15).
           
       FD REORDER-REPORT-OUT.
       01 REORDER-DETAIL PIC X(65).

       WORKING-STORAGE SECTION.
       01 INDICATORS.
           05 EOF PIC X(3) VALUE "NO ".
           05 EOF-SUP PIC X(3) VALUE "NO ".
           05 NEED-REORDER PIC X(3) VALUE "NO ".
           05 FOUND-FLAG PIC X(3) VALUE "NO ".
           05 SUB-2 PIC 99.
           05 SUB PIC 99.
           05 COUNTER-MODULUS PIC 9(2).
           05 HELPER-NUMBER PIC 9(2).
           05 FOUND-SUPPLIER-NAME PIC X(15).
           
       01 NON-FORMATED-NUMBERS.
           05 TOTAL-VALUE-WS PIC 9(10) VALUE ZEROS.
           05 ITEM-VALUE-WS PIC 9(7).
           05 READ-COUNTER PIC 9(5) VALUE 0.
           05 WRITE-COUNTER PIC 9(5) VALUE 0.
           
       01 SUPPLIER-TABLE.
           05 TABLE-RECORD OCCURS 15 TIMES.
               10 T-SUPPLIER-CODE PIC X(5).
               10 T-SUPPLIER-NAME PIC X(15).
               
       01 COLUMN-HEADER.
           05 FILLER PIC X VALUE SPACE.
           05 FILLER PIC X(6) VALUE "NUMBER".
           05 FILLER PIC X VALUE SPACE.
           05 FILLER PIC X(8) VALUE "PARTNAME".
           05 FILLER PIC X(17) VALUE SPACES.
           05 FILLER PIC X(3) VALUE "QTY".
           05 FILLER PIC X(3) VALUE SPACES.
           05 FILLER PIC X(5) VALUE "VALUE".
           05 FILLER PIC X(6) VALUE SPACES.
           
       01 REORDER-COLUMN-HEADER.
           05 FILLER PIC X VALUE SPACE.
           05 FILLER PIC X(6) VALUE "NUMBER".
           05 FILLER PIC X VALUE SPACE.
           05 FILLER PIC X(8) VALUE "PARTNAME".
           05 FILLER PIC X(17) VALUE SPACES.
           05 FILLER PIC X(3) VALUE "QTY".
           05 FILLER PIC X(3) VALUE SPACES.
           05 FILLER PIC X(6) VALUE "REOPNT".
           05 FILLER PIC X VALUE SPACE.
           05 FILLER PIC X(7) VALUE "SUPNAME".
           05 FILLER PIC X(9) VALUE SPACES.
           
       01 RECORD-LINE.
           05 FILLER PIC X VALUE SPACE.
           05 PART-NUMBER-WS PIC 9(5).
           05 FILLER PIC X(2) VALUE SPACES.
           05 PART-NAME-WS PIC X(20).
           05 FILLER PIC X(5) VALUE SPACES.
           05 QUANTITY-ON-HAND-WS PIC ZZ9.
           05 FILLER PIC X(3) VALUE SPACES.
           05 ITEM-VALUE-DISPLAY PIC ZZ,ZZZ.99.
           05 FILLER PIC X(4) VALUE SPACES.
           
       01 REORDER-RECORD-LINE.
           05 FILLER PIC X VALUE SPACE.
           05 REORDER-PART-NUMBER-WS PIC 9(5).
           05 FILLER PIC X(1) VALUE SPACES.
           05 REODER-PART-NAME-WS PIC X(20).
           05 FILLER PIC X(5) VALUE SPACES.
           05 REORDER-QUANTITY-WS PIC ZZ9.
           05 FILLER PIC X(3) VALUE SPACES.
           05 REORDER-POINT-WS PIC ZZ9.
           05 FILLER PIC X(4) VALUE SPACES.
           05 SUPPLIER-NAME-WS PIC X(15).
           
       01 TOTAL-VALUE-LINE.
           05 FILLER PIC X VALUE SPACES.
           05 FILLER PIC X(10) VALUE "TOTALVALUE".
           05 FILLER PIC X(2) VALUE SPACES.
           05 TOTAL-VALUE-DISPLAY PIC $$,$$$,$$$.99.
           
       01 RECORDS-READ-LINE.
           05 FILLER PIC X VALUE SPACES.
           05 FILLER PIC X(7) VALUE "RECORDS".
           05 FILLER PIC X(1) VALUE SPACES.
           05 FILLER PIC X(4) VALUE "READ".
           05 FILLER PIC X(4) VALUE SPACES.
           05 RECORDS-READ-WS PIC ZZ,ZZ9.
           
       01 RECORDS-WRITTEN-LINE.
           05 FILLER PIC X VALUE SPACES.
           05 FILLER PIC X(7) VALUE "RECORDS".
           05 FILLER PIC X VALUE SPACES.
           05 FILLER PIC X(7) VALUE "WRITTEN".
           05 FILLER PIC X VALUE SPACE.
           05 RECORDS-WRITTEN-WS PIC ZZ,ZZ9.

       PROCEDURE DIVISION.
       
       100-CREATE-INVENTORY-REPORT.
           PERFORM 201-INITIATE-CREATE-INVENTORY-REPORT.
           PERFORM 202-CREATE-INVENTORY-RECORD UNTIL EOF = "YES".
           PERFORM 203-TERMINATE-CREATE-INVENTORY-REPORT.
           STOP RUN.
           
       201-INITIATE-CREATE-INVENTORY-REPORT.
           PERFORM 301-OPEN-FILES.
           PERFORM 302-LOAD-SUPPLIER-TABLE 
               VARYING SUB FROM 1 BY 1 
                   UNTIL SUB > 15
                       OR EOF-SUP = "YES".
           PERFORM 303-WRITE-HEADERS.
           PERFORM 304-READ-RECORD.
       
       202-CREATE-INVENTORY-RECORD.
           PERFORM 305-CALCULATE-ITEM-VALUE.
           PERFORM 306-ADD-VALUE-TO-TOTAL.
           PERFORM 307-FORMAT-NUMBERS.
           PERFORM 308-WRITE-TO-INVENTORY-REPORT-OUT.
           PERFORM 309-CHECK-REORDER.
           IF NEED-REORDER = "YES"
               PERFORM 310-WRITE-TO-REORDER-REPORT-OUT
           END-IF.
           PERFORM 304-READ-RECORD.
       
       203-TERMINATE-CREATE-INVENTORY-REPORT.
           PERFORM 311-CREATE-REPORT-ENDING.
           PERFORM 312-CLOSE-FILES.
       
       301-OPEN-FILES.
           OPEN INPUT INVENTORY-FILE-IN.
           OPEN OUTPUT INVENTORY-REPORT-OUT.
           OPEN INPUT SUPPLIER-FILE-IN.
           OPEN OUTPUT REORDER-REPORT-OUT.
       
       302-LOAD-SUPPLIER-TABLE.
           READ SUPPLIER-FILE-IN
               AT END MOVE "YES" TO EOF-SUP
               NOT AT END MOVE SUPPLIER-RECORD TO TABLE-RECORD(SUB).
           
       303-WRITE-HEADERS.
           PERFORM 401-WRITE-INVRPT-HEADER.
           PERFORM 402-WRITE-REORPT-HEADER.
           
       304-READ-RECORD.
           READ INVENTORY-FILE-IN
               AT END MOVE "YES" TO EOF
                   NOT AT END ADD 1 TO READ-COUNTER.
                   
       305-CALCULATE-ITEM-VALUE.
           COMPUTE ITEM-VALUE-WS = QUANTITY-ON-HAND * UNIT-PRICE.
           
       306-ADD-VALUE-TO-TOTAL.
           COMPUTE TOTAL-VALUE-WS = TOTAL-VALUE-WS + ITEM-VALUE-WS.
           
       307-FORMAT-NUMBERS.
           MOVE TOTAL-VALUE-WS TO TOTAL-VALUE-DISPLAY.
           MOVE ITEM-VALUE-WS TO ITEM-VALUE-DISPLAY.
           MOVE PART-NUMBER TO PART-NUMBER-WS.
           MOVE PART-NAME TO PART-NAME-WS.
           MOVE QUANTITY-ON-HAND TO QUANTITY-ON-HAND-WS.
           
       308-WRITE-TO-INVENTORY-REPORT-OUT.
           DIVIDE READ-COUNTER BY 11 
               GIVING HELPER-NUMBER REMAINDER COUNTER-MODULUS.
           IF COUNTER-MODULUS EQUALS 0 THEN                       
               PERFORM 404-WRITE-RECORD-NEW-PAGE
           ELSE
               PERFORM 403-WRITE-RECORD
           END-IF.
           ADD 1 TO WRITE-COUNTER.
           
       309-CHECK-REORDER.
           MOVE "NO " TO NEED-REORDER.
           IF QUANTITY-ON-HAND < REORDER-POINT
               MOVE "YES" TO NEED-REORDER
           END-IF.
           
       310-WRITE-TO-REORDER-REPORT-OUT.
           MOVE "NO" TO FOUND-FLAG.
           PERFORM 405-SEARCH-SUPPLIER-NAME VARYING SUB-2
               FROM 1 BY 1 UNTIL FOUND-FLAG ="YES"
               OR SUB-2 > 10.
           PERFORM 406-WRITE-REORDER-RECORD.
       
       311-CREATE-REPORT-ENDING.
           PERFORM 407-WRITE-TOTAL-VALUE-TO-REPORT-OUT.
           PERFORM 408-WRITE-NUMBER-OF-RECORD-READ.
           PERFORM 409-WRITE-NUMBER-OF-RECORD-WRITTEN.
       
       312-CLOSE-FILES.
           CLOSE INVENTORY-FILE-IN.
           CLOSE INVENTORY-REPORT-OUT.
           CLOSE SUPPLIER-FILE-IN.
           CLOSE REORDER-REPORT-OUT.

       401-WRITE-INVRPT-HEADER.
           WRITE REPORT-CONTENT FROM COLUMN-HEADER
               BEFORE ADVANCING 2 LINES.
               
       402-WRITE-REORPT-HEADER.
           WRITE REORDER-DETAIL FROM REORDER-COLUMN-HEADER
               BEFORE ADVANCING 2 LINES.
       
       403-WRITE-RECORD.
           WRITE REPORT-CONTENT FROM RECORD-LINE.
           
       404-WRITE-RECORD-NEW-PAGE.
           WRITE REPORT-CONTENT 
               FROM COLUMN-HEADER AFTER PAGE.
           WRITE REPORT-CONTENT 
               FROM RECORD-LINE AFTER ADVANCING 2 LINES.
       
       405-SEARCH-SUPPLIER-NAME.
           IF INV-SUPPLIER-CODE = T-SUPPLIER-CODE(SUB-2)
               MOVE T-SUPPLIER-NAME(SUB-2)
                   TO FOUND-SUPPLIER-NAME
                   MOVE "YES" TO FOUND-FLAG.
       
       406-WRITE-REORDER-RECORD.
           MOVE PART-NUMBER TO REORDER-PART-NUMBER-WS.
           MOVE PART-NAME TO REODER-PART-NAME-WS.
           MOVE QUANTITY-ON-HAND TO REORDER-QUANTITY-WS.
           MOVE REORDER-POINT TO REORDER-POINT-WS.
           MOVE FOUND-SUPPLIER-NAME TO SUPPLIER-NAME-WS.
           WRITE REORDER-DETAIL FROM REORDER-RECORD-LINE.

       407-WRITE-TOTAL-VALUE-TO-REPORT-OUT.
           WRITE REPORT-CONTENT FROM TOTAL-VALUE-LINE 
               AFTER ADVANCING 2 LINES.
       
       408-WRITE-NUMBER-OF-RECORD-READ.
           MOVE READ-COUNTER TO RECORDS-READ-WS.
           WRITE REPORT-CONTENT FROM RECORDS-READ-LINE
               AFTER ADVANCING 2 LINES.
       
       409-WRITE-NUMBER-OF-RECORD-WRITTEN.
           MOVE WRITE-COUNTER TO RECORDS-WRITTEN-WS.
           WRITE REPORT-CONTENT FROM RECORDS-WRITTEN-LINE.

       
